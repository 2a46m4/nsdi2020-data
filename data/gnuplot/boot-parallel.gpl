# Plot parallel boot data for the paper
#
# invoke with:
# gnuplot -e "base='<dir>'" boot-parallel.gpl
# where <dir> is the platform directory


fc_dat =        sprintf("%s/boot-times-parallel-100-fc-cdf.dat", base)
qboot_dat =     sprintf("%s/boot-times-parallel-100-qemu-cdf.dat", base)

out_file = sprintf("%s/boot-parallel.pdf", base)


set terminal pdf color solid enhanced
set output out_file

set xlabel 'Boot time (ms)'
set ylabel 'CDF'

set xrange [0:]
set yrange [0:1.05]
set offsets graph 0, 0, 0.01, 0.01

set key top left

set mytics 4
set grid ytics mytics xtics mxtics

plot \
     fc_dat          index 1 using 1:2         w l lw 2 t 'AnonVMM-pre', \
     fc_dat          index 0 using ($1/1000):2 w l lw 2 t 'AnonVMM', \
     qboot_dat       index 0 using ($1/1000):2 w l lw 2 t 'Qemu', \
